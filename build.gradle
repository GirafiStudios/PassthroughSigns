buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: false
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

ext.buildnumber=""
if (System.getenv().BUILD_NUMBER) {
    ext.buildnumber = ".B" + System.getenv().BUILD_NUMBER
}

version = "${minecraft_version}-${mod_version}${buildnumber}"
group = "com.girafi.passthroughsigns"
archivesBaseName = "PassthroughSigns"

minecraft {
    mappings channel: 'official', version: project.minecraft_version

    runs {
        client = {
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
}

jar {
    manifest {
        attributes([
            "Implementation-Version": "${version}"
        ])
    }
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

task apiJar(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.main.output
    include 'com/girafi/passthroughsigns/api/**/*'
    classifier = 'api'
}

artifacts {
    archives sourcesJar
    archives apiJar
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId "passthroughsigns_${minecraft_version}"
            artifact jar
			artifact sourcesJar
			artifact apiJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}