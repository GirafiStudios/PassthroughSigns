buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

ext.buildnumber=""
if (System.getenv().BUILD_NUMBER) {
    ext.buildnumber = ".B" + System.getenv().BUILD_NUMBER
}

version = "${minecraft_version}-${mod_version}${buildnumber}"
group = "com.girafi.passthroughsigns"
archivesBaseName = "PassthroughSigns"

minecraft {
    runDir = "run"
    mappings = "${mappings_version}"
    version = "${minecraft_version}-${forge_version}"

    replace "%MOD_VERSION%", "${mod_version}"
    replaceIn "Reference.java"

    replace "%API_VERSION%", "${api_version}"
    replaceIn "package-info.java"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId "passthroughsigns_${minecraft_version}"
            artifact jar
			artifact sourcesJar
			artifact apiJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

task apiJar(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.main.output
    include 'com/girafi/passthroughsigns/api/**/*'
    classifier = 'api'
}

artifacts {
    archives sourcesJar
    archives apiJar
}